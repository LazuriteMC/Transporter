plugins {
    id 'com.github.johnrengelman.shadow' version "$shadow_version"
    id 'dev.architectury.loom' version "$loom_version"
    id 'maven-publish'
}

loom.silentMojangMappingsLicense()
version mod_version
group maven_group

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }

    withSourcesJar()
}

repositories {
    maven {
        name 'Lazurite'
        url 'https://lazurite.dev/releases'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings loom.officialMojangMappings()
    modApi "dev.lazurite:toolbox:$toolbox_version", { transitive false }
    annotationProcessor "org.spongepowered:mixin:$mixin_version:processor"
}


shadowJar {
    configurations = [project.configurations.shadow]
}

remapJar {
    dependsOn shadowJar
    input.set shadowJar.archiveFile
}

publishing {
    repositories {
        maven {
            name 'Lazurite'
            url 'https://lazurite.dev/releases'

            credentials {
                username property('publish.lazurite.alias')
                password property('publish.lazurite.secret')
            }
        }
    }

    publications {
        transporter(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
        }
    }
}