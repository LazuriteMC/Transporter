plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings minecraft.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation(fabricApi.module("fabric-networking-api-v1", project.fabric_version))
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 16
}

java {
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'transporter'

            pom {
                name = 'Transporter'
                description = 'Beam model info to the server.'
                url = 'https://github.com/LazuriteMC/Transporter'
                developers {
                    developer {
                        id = 'bluevista'
                        name = 'BlueVista'
                        email = 'ethan@ejohnsons.com'
                    }
                }
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            artifact(remapJar) {
                builtBy remapJar
            }

            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        maven {
            url = 'https://lazurite.dev/maven/releases'
            credentials {
                username = project.property("publish.lazurite.alias")
                password = project.property("publish.lazurite.secret")
            }
        }
    }
}
